extends Node


const SAVE_PATH: String = "user://bestscore.bin"


# score is 1-based level
# current = may be set by game manager or not
# best = last level completed

var best_score: int = 0
var current_score: int = 0


func _init() -> void:
	_load()


## Loads score or set to default
func _load() -> void:
	var file: FileAccess = FileAccess.open(SAVE_PATH, FileAccess.READ)
	if not file:
		var err: Error = FileAccess.get_open_error()
		if err != ERR_FILE_NOT_FOUND: printerr("[Persistence]: loading error:", FileAccess.get_open_error())
		return
	# NOTE: data = JSON.parse_string(file.get_as_text())
	best_score = file.get_32()
	file.close()


## Saves best score from memory to file
func _save() -> void:
	var file: FileAccess = FileAccess.open(SAVE_PATH, FileAccess.WRITE)
	if not file:
		push_error("[Persistence]: saving error:", FileAccess.get_open_error())
		return
	# NOTE: file.store_string(JSON.stringify(data))
	if not file.store_32(best_score):
		push_error("[Persistence]: saving error:", FileAccess.get_open_error())
	file.close()


## Updates best score if was beaten and saves  into file if it was
func submit() -> void:
	if current_score <= best_score: return
	best_score = current_score
	_save()


## Resets best score back to 0 both in memory and in file
func reset() -> void:
	print("[Peresistence]: progress was reset")

	best_score = -1
	current_score = 0
	submit()
